// Get an image's hash tag
String getImageTagHash(String imageName, String tag = "") {

  if(!tag?.trim()) {
    tag = "latest"
  }

  def istag = openshift.raw("get istag ${imageName}:${tag} -o template --template='{{.image.dockerImageReference}}'")
  return istag.out.tokenize('@')[1].trim()
}

node {
  stage("Init") {
    script {
      dir(''){
        checkout scm
        config = load 'jenkins/config.groovy'
      }
    }
  }

  def appLabel = config.APP_LABEL

  stage("Building images ...") {
    script {
      openshift.withCluster() {
        openshift.withProject() {

          // Find all of the build configurations associated to the application using labels ...
          def buildconfigs = openshift.selector("bc", [ app : "${appLabel}" ])
          echo "Found ${buildconfigs.count()} buildconfigs for app label (app=${appLabel}): ${buildconfigs.names()}"

          // Kick off all the builds in parallel ...
          def builds = buildconfigs.startBuild()
          echo "Started ${builds.count()} builds: ${builds.names()}"

          timeout(config.WAIT_TIMEOUT) {
            // Wait for all the builds to complete ...
            // This section will exit after the last build completes.
            echo "Waiting for builds to complete ..."
            builds.withEach {
              // untilEach and watch - do not support watching multiple named resources,
              // so we have to feed it one at a time.
              it.untilEach(1) {
                  echo "${it.object().status.phase} - ${it.name()}"
                  return (it.object().status.phase == "Complete")
              }
            }
          }

          echo "Builds complete ..."
        }
      }
    }
  }

  def destination = config.DEV_ENV
  def namespace = config.NAME_SPACE
  def appSuffix = config.SUFFIX

  config.APPS.each { appName ->
    stage("Deploying ${appName} to ${destination}") {
      script {
        openshift.withCluster() {
          openshift.withProject() {
            // Don't tag with BUILD_ID so the pruner can do it's job; it won't delete tagged images.
            // Tag the images for deployment based on the image's hash
            def source = getImageTagHash("${appName}")
            echo "Tagging ${appName}@${source} for deployment to ${destination} ..."
            openshift.tag("${appName}@${source}", "${appName}:${destination}")
          }

          echo "Watching rollout of ${appName}${appSuffix} in ${namespace}-${destination} ..."
          openshift.withProject("${namespace}-${destination}") {
              def dc = openshift.selector('dc', "${appName}${appSuffix}")
              // Wait for the deployment to complete.
              // This will wait until the desired replicas are all available
              dc.rollout().status()
          }

          echo "Deployment Complete."
        }
      }
    }
  }
}